// SQL Grammar for Chronos Database

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "--" ~ (!"\n" ~ ANY)* ~ "\n"? }

// Keywords
CREATE = { ^"CREATE" }
TABLE = { ^"TABLE" }
INSERT = { ^"INSERT" }
INTO = { ^"INTO" }
VALUES = { ^"VALUES" }
SELECT = { ^"SELECT" }
FROM = { ^"FROM" }
WHERE = { ^"WHERE" }
AND = { ^"AND" }
OR = { ^"OR" }
NOT = { ^"NOT" }
NULL = { ^"NULL" }
INT = { ^"INT" }
TEXT = { ^"TEXT" }
FLOAT = { ^"FLOAT" }
BOOLEAN = { ^"BOOLEAN" }
PRIMARY = { ^"PRIMARY" }
KEY = { ^"KEY" }
STRING = { ^"STRING" }
BEGIN = { ^"BEGIN" }
COMMIT = { ^"COMMIT" }
ROLLBACK = { ^"ROLLBACK" }
TRANSACTION = { ^"TRANSACTION" }
UPDATE = { ^"UPDATE" }
SET = { ^"SET" }
DELETE = { ^"DELETE" }
INDEX = { ^"INDEX" }
ON = { ^"ON" }
WITH = { ^"WITH" }
TTL = { ^"TTL" }
AVG_1H = { ^"AVG_1H" }
AVG_24H = { ^"AVG_24H" }
AVG_7D = { ^"AVG_7D" }
COUNT = { ^"COUNT" }
SUM = { ^"SUM" }
AVG = { ^"AVG" }
MIN = { ^"MIN" }
MAX = { ^"MAX" }
GROUP = { ^"GROUP" }
BY = { ^"BY" }
JOIN = { ^"JOIN" }
USING = { ^"USING" }

// Identifiers
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
quoted_identifier = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
table_name = { identifier | quoted_identifier }
column_name = { identifier | quoted_identifier }

// Data types
data_type = { INT | TEXT | FLOAT | BOOLEAN | STRING }

// Literals
string_literal = @{ "'" ~ (!"'" ~ ANY)* ~ "'" }
integer_literal = @{ ASCII_DIGIT+ }
float_literal = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* | "." ~ ASCII_DIGIT+ }
boolean_literal = @{ ^"TRUE" | ^"FALSE" }
null_literal = @{ NULL }
literal = { string_literal | float_literal | integer_literal | boolean_literal | null_literal }

// TTL
number_literal = @{ ASCII_DIGIT+ }
unit = { "s" | "m" | "h" | "d" }
ttl_value = { number_literal ~ unit? }
ttl_clause = { WITH ~ TTL ~ "=" ~ ttl_value }

// Operators
comparison_operator = { "=" | "!=" | "<" | ">" | "<=" | ">=" }

// Expressions
column_definition = { column_name ~ data_type ~ (PRIMARY ~ KEY)? }
column_list = { "(" ~ column_definition ~ ("," ~ column_definition)* ~ ")" }
value = { literal | "?" }
value_list = { "(" ~ value ~ ("," ~ value)* ~ ")" }
column_selector = { "*" | column_name ~ ("," ~ column_name)* }
condition = { column_name ~ comparison_operator ~ value }
where_clause = { WHERE ~ condition ~ ((AND | OR) ~ condition)* }
group_by_clause = { GROUP ~ BY ~ column_name }
assignment = { column_name ~ "=" ~ value }
assignment_list = { assignment ~ ("," ~ assignment)* }

// Statements
create_table_stmt = { CREATE ~ TABLE ~ table_name ~ column_list ~ ttl_clause? ~ ";" }
create_index_stmt = { CREATE ~ INDEX ~ identifier ~ ON ~ table_name ~ "(" ~ column_name ~ ")" ~ ";" }
insert_stmt = { INSERT ~ INTO ~ table_name ~ ("(" ~ column_name ~ ("," ~ column_name)* ~ ")")? ~ VALUES ~ value_list ~ ";" }
select_agg_1h_stmt = { SELECT ~ AVG_1H ~ "(" ~ column_name ~ ")" ~ FROM ~ table_name ~ ";" }
select_agg_24h_stmt = { SELECT ~ AVG_24H ~ "(" ~ column_name ~ ")" ~ FROM ~ table_name ~ ";" }
select_agg_7d_stmt = { SELECT ~ AVG_7D ~ "(" ~ column_name ~ ")" ~ FROM ~ table_name ~ ";" }
select_count_stmt = { SELECT ~ COUNT ~ "(" ~ ("*" | column_name) ~ ")" ~ FROM ~ table_name ~ where_clause? ~ ";" }
select_sum_stmt = { SELECT ~ SUM ~ "(" ~ column_name ~ ")" ~ FROM ~ table_name ~ where_clause? ~ ";" }
select_avg_stmt = { SELECT ~ AVG ~ "(" ~ column_name ~ ")" ~ FROM ~ table_name ~ where_clause? ~ ";" }
select_min_stmt = { SELECT ~ MIN ~ "(" ~ column_name ~ ")" ~ FROM ~ table_name ~ where_clause? ~ ";" }
select_max_stmt = { SELECT ~ MAX ~ "(" ~ column_name ~ ")" ~ FROM ~ table_name ~ where_clause? ~ ";" }
select_group_count_stmt = { SELECT ~ column_name ~ "," ~ COUNT ~ "(" ~ "*" ~ ")" ~ FROM ~ table_name ~ where_clause? ~ group_by_clause ~ ";" }
select_join_stmt = { SELECT ~ column_selector ~ FROM ~ table_name ~ JOIN ~ table_name ~ USING ~ "(" ~ column_name ~ ")" ~ where_clause? ~ ";" }
select_stmt = { SELECT ~ column_selector ~ FROM ~ table_name ~ where_clause? ~ ";" }
begin_stmt = { BEGIN ~ TRANSACTION? ~ ";" }
commit_stmt = { COMMIT ~ ";" }
rollback_stmt = { ROLLBACK ~ ";" }

// Statements
update_stmt = { UPDATE ~ table_name ~ SET ~ assignment_list ~ where_clause? ~ ";" }
delete_stmt = { DELETE ~ FROM ~ table_name ~ where_clause? ~ ";" }

// SQL statement
sql_stmt = { create_table_stmt
                 | create_index_stmt
                 | insert_stmt
                 | select_agg_1h_stmt
                 | select_agg_24h_stmt
                 | select_agg_7d_stmt
                 | select_count_stmt
                 | select_sum_stmt
                 | select_avg_stmt
                 | select_min_stmt
                 | select_max_stmt
                 | select_group_count_stmt
                 | select_join_stmt
                 | select_stmt
                 | begin_stmt
                 | commit_stmt
                 | rollback_stmt
                 | update_stmt
                 | delete_stmt }

// Entry point
sql = { SOI ~ sql_stmt ~ EOI }